title: Red Guards
new-page: true
is-card: true
tags: para_affairs
view-if: red_guards_timer <= 0
card-image: img/RedGuards.jpg
on-arrival: week_actions += 1; red_guards_timer = 3; rg_militarisation_cost = 2 if rg_militancy > 0.5 else rg_militarisation_cost = 1; rg_militarisation_cost = 3 if rg_militancy > 0.75

= Red Guards

The *Guardie Rosse*, also known as the Red Guards is a paramilitary organisation affiliated with our party. We have historically supported the party, having seen it as the *de facto* paramilitary wing of the party and utilising the organisation to advance our goals.

- @militant: Fund more military-style training and equipment for the Red Guards.
- @militant_2: Use our existing resources to support more training for the Red Guards.
- @same: Keep the Red Guards as it is.
- @cut_rg: Cut funding for the Red Guards.
- @easy_discard

@militant
choose-if: para_resources >= rg_militarisation_cost
unavailable-subtitle: Not enough paramilitary resources ([+ rg_militarisation_cost +] needed).
subtitle: -[+ rg_militarisation_cost +] paramilitary resources
on-arrival: para_resources -= rg_militarisation_cost; rg_investment += 1; rg_strength += 15; rg_militancy += 0.05; communist_strength += 2; communist_dissent -= 2; reformist_dissent += 5; radicalisation += 0.1; workers_psi += 2*(1-dissent); veterans_psi += 2*(1-dissent);

We continue funding the Red Guards, who begin recruitment of more members and are able to acquire more effective weaponry.


@militant_2
view-if: rg_investment >= 1
on-arrival: rg_militancy += 0.02; rg_investment -= 1

As we have already funded resources towards the Red Guards, we can invest in building up their militancy.


@same

The Red Guards is already serving the purpose it needs to serve. There is no reason to change what already works.

- @return: Continue...

@cut_rg
subtitle: +1 paramilitary resources
on-arrival: para_resources += 1; rg_strength -= 15; rg_militancy -= 0.05; communist_strength -= 2; communist_dissent -= 2; radicalisation -= 0.05; workers_pnf += 2; veterans_psri += 2

Cutting resources to the Red Guards causes a drop in the membership, and leads some would-be members to join the PSRI or the PNF instead.