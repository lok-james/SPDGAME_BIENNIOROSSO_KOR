title: 정치 집회
new-page: true
is-card: true
tags: party_affairs
view-if: rally_timer <= 0
on-arrival: week_actions += 1; rally_timer = 4; disrupt_strength = ((fdc_strength*fdc_militancy) + (ard_strength*ard_militancy) + (abl_strength*abl_militancy))
card-image: img/Rally.jpg
go-to: main_rally; far_right_disrupt if prime_minister_party = "PNF" and (fdc_banned = 0 and ard_banned = 0 and abl_banned = 0) and not return_to_normalcy

@far_right_disrupt

극우 준군사조직이 우리 집회를 방해하고 있습니다! 그들의 병력이 우리 집회를 막고 있습니다.

- @rg_defend: 적위대를 동원해 집회를 보호한다.
- @adp_defend: 인민의 결사대를 동원해 집회를 보호한다.
- @both_defend: 적위대와 인민의 결사대가 협력하여 집회를 보호한다.
- @cancel: 집회를 취소한다.

@rg_defend
choose-if: not rg_banned
unavailable-subtitle: 적위대는 활동이 금지되었습니다.
on-arrival: rg_defend_success = rg_strength*rg_militancy - disrupt_strength
go-to: rg_protect_win if rg_defend_success >= 0; rg_protect_lose if rg_defend_success < 0

@rg_protect_win 
go-to: main_rally

적위대가 성공적으로 우리 집회를 지켜냈으므로, 예정대로 진행할 수 있습니다.

@rg_protect_lose
on-arrival: workers_pnf += 3

안타깝게도 적위대는 극우 폭도들을 막아내지 못했습니다. 이번 힘겨루기에서는 파시스트들이 승리했습니다.

@adp_defend
view-if: arditi_cooperation
on-arrival: adp_defend_success = adp_strength*adp_militancy - disrupt_strength
go-to: adp_protect_win if adp_defend_success >= 0; adp_protect_lose if adp_defend_success < 0

@adp_protect_win 
go-to: main_rally

인민의 결사대가 성공적으로 우리 집회를 지켜냈으므로, 예정대로 진행할 수 있습니다.

@adp_protect_lose
on-arrival: workers_pnf += 3

안타깝게도 인민의 결사대는 극우 폭도들을 막아내지 못했습니다. 이번 힘겨루기에서는 파시스트들이 승리했습니다.

@both_defend
view-if: arditi_cooperation
choose-if: arditi_rg_cooperation
unavailable-subtitle: 인민의 결사대는 적위대와 협력한 전적이 없습니다.
on-arrival: both_defend_success = ((adp_strength*adp_militancy) + (rg_strength*rg_militancy)) - disrupt_strength
go-to: both_protect_win if both_defend_success >= 0; both_protect_lose if both_defend_success < 0

@both_protect_win 
go-to: main_rally

적위대와 인민의 결사대가 성공적으로 우리 집회를 지켜냈으므로, 예정대로 진행할 수 있습니다.

@both_protect_lose
on-arrival: workers_pnf += 3

안타깝게도 적위대와 인민의 결사대는 극우 폭도들을 막아내지 못했습니다. 이번 힘겨루기에서는 파시스트들이 승리했습니다.

@cancel
on-arrival: workers_pnf += 3

이번 힘겨루기에서는 극우파가 승리했습니다.

@main_rally

= 정치 집회

대중 집회는 사회민주주의적 생활의 일상적인 활동이며, 특히 선거를 앞두고 더욱 그렇습니다. 이번 집회의 주요 주제는 무엇입니까?

- @class_struggle: 부르주아 계급에 맞선 프롤레타리아 계급의 투쟁.
- @welfare: 전후의 어려움 속에서의 사회 복지 프로그램 유지 및 확대.
- @democracy: 정부 민주화를 지속하고 현 군주주의자들과 파시스트들에 맞선 투쟁.
- @pacifism: 어떤 대가를 치르더라도 전쟁, 민족주의, 군국주의에 반대.
- @economic: 현 경제 위기와 정부의 무능 종식.
- @local: 청중에게 가장 큰 반향을 일으키는 다양한 지역, 국가 현안의 조합.
- @easy_discard
- @cancel_advisor_action

@class_struggle
on-arrival: workers_psi += 4*(1-dissent); veterans_psi += 3*(1-dissent) if communist_strength > reformist_strength; workers_psi += 4*(1-dissent) if nationalisation_progress > 0 or max_seize > 0; veterans_psi += 3*(1-dissent) if nationalisation_progress > 0; communist_strength += 5; communist_dissent -= 5; middle_class_psi -= 3; industrialists_psi -= 2; revolutionary_fervour += 3

우리의 계급 투쟁 메시지는 노동 계급 좌파에게는 호소력이 있지만, 중산층을 소외시킬 수 있습니다. [? if nationalisation_progress > 0 : 우리의 국유화 성과가 이 메시지에 힘을 실어주었습니다. ?]

@welfare
on-arrival: veterans_psi += 3*(1-dissent); workers_psi += 3*(1-dissent) if welfare > 0; veterans_psi += 3*(1-dissent) if welfare > 0; veterans_psi += 4*(1-dissent) if welfare > 1; reformist_strength += 3; labour_strength += 4; reformist_dissent -= 3; labour_dissent -= 3;

개혁주의적 메시지는 사회민주주의의 성과로 혜택을 본 조직 노동자들에게 호소력이 있습니다. [? if welfare > 1 : 우리가 이룬 복지 확대 성과가 이 메시지를 더욱 효과적으로 만들었습니다. ?]

@democracy
on-arrival: democratisation += 1; republican_sentiment += 2*(1 - dissent); workers_psi += 4*(1-dissent) if democratisation >= 3; veterans_psi += 3*(1-dissent) if democratisation >= 3; socialist_sentiment += 1*(1 - dissent) if democratisation >= 3; middle_class_psi += 4*(1-dissent) if democratisation >= 3; republican_sentiment += 4*(1 - dissent) if democratisation >= 3; king_relation -= 3; king_relation -= 4 if democratisation >= 3; radicalisation -= 0.1; radicalisation += 0.05 if democratisation >= 3; republican_sentiment += 2 if king_event; socialist_sentiment += 2 if king_event; workers_psi += 3 if king_event;
view-if: not king_abolished

우리 연사들은 부패한 체제에 맞서, 체제를 유지하는 군주주의자들과 권위주의자들을 맹렬히 비난합니다. [? if king_event : 우리를 제거하려는 국왕이 선포한 현 비상사태로 인해 우리의 노력은 더욱 힘을 얻고 있습니다. ?]

[? if democratisation < 3 : 민주주의에 대한 담론은 보다 현실적인 문제만큼 노동자들의 관심을 끌지 못합니다. 하지만 상황은 바뀔 수 있습니다. 프롤레타리아에게 정치 교육을 제공하는 것도 우리의 임무 중 하나입니다. ?]
[? if democratisation >= 3 : 우리가 꾸준히 민주화를 유세의 주제로 삼았기 때문에, 노동자들은 우리 메시지에 열광하고 있습니다. ?]

@pacifism
on-arrival: pacifism += 1; nationalist_sentiment -= 3*(1 - dissent); workers_psi += 4*(1 - dissent) if pacifism >= 3; veterans_psi += 3*(1-dissent) if pacifism >= 3; nationalist_sentiment -= 3*(1-dissent) if pacifism >= 3; middle_class_psi += 4*(1-dissent) if pacifism >= 3

우리 연사들은 군부와 그 밑 빠진 독에 쏟아붓는 모든 지출을 규탄합니다. 일부 과감한 연사들은 대전쟁을 일으킨 군 엘리트들의 책임에 의문을 제기합니다.

[? if pacifism < 3 : 사회에 만연한 민족주의적 세뇌를 생각하면, 군에 대해 이토록 격렬하게 반대 주장을 펴기는 어렵습니다. ?]
[? if pacifism >= 3 : 우리가 꾸준히 평화주의를 유세의 주제로 삼았기 때문에, 노동자들은 우리 메시지에 열광하고 있습니다. ?]

@economic
view-if: not psi_in_government and economic_plan = 1 and pnf_in_government = 0
choose-if: unemployment > 5 or economic_growth < 0 or national_economic_recovery 
unavailable-subtitle: 경제 위기가 거의 끝났기 때문에, 대중은 더 이상 이 문제에 관심이 없습니다.
on-arrival: workers_psi += 4*(1-dissent); veterans_psi += 3*(1-dissent); middle_class_psi += 1*(1-dissent); ppi_relation -= 4*(1-dissent); ldr_relation -= 4*(1-dissent); ul_relation -= 4*(1-dissent); ds_relation -= 4*(1-dissent); workers_psi += 1*(1-dissent) if unemployment > 10; veterans_psi += 3*(1-dissent) if unemployment > 10;

우리의 반정부 메시지는 노동자와 재향군인의 공감을 얻고 있지만, 연정 참여 정당들은 이를 달가워하지 않는 것 같습니다.

@local
on-arrival: workers_psi += 3*(1-dissent); middle_class_psi += 2*(1-dissent); rural_psi += 1*(1-dissent); veterans_psi += 2*(1-dissent);

우리 연사들은 분위기를 파악하여, 지금 이 순간 노동자들에게 가장 큰 영향을 미치는 문제들을 연설하고 있습니다.
