title: 당 조직
new-page: true
is-card: true
tags: party_affairs
view-if: party_organisations_timer <= 0
on-arrival: week_actions += 1; party_organisations_timer = 3;
card-image: img/PartyOrganisations.jpg
max-visits: 15

= 당 조직

[+ psi_name +]는 정당일 뿐만 아니라, 요람에서 무덤까지 당원들의 사회적 필요를 충족시키는 방대한 관련 조직망을 갖춘 그 자체로 하나의 사회 공동체입니다.

- @newspapers: 더 많은 신문사와 기자에게 자금을 지원한다.
- @welfare: 더 많은 상호 부조와 복지 프로그램에 자금을 지원한다.
- @culture: 스포츠 리그, 교육, 문화 프로그램에 자금을 지원한다.
- @rg: 우리의 준군사 조직인 적위대에 자금을 지원한다.
- @nothing: 어떤 프로그램에도 추가 자금 지원을 하지 않는다.
- @easy_discard
- @cancel_advisor_action

@newspapers
choose-if: resources > 0
unavailable-subtitle: 자원이 부족합니다. (1 필요)
subtitle: -1 자원
on-arrival: resources -= 1; media_timer = 0;
go-to: media

@welfare
choose-if: resources > 0
unavailable-subtitle: 자원이 부족합니다. (1 필요)
subtitle: -1 자원
on-arrival: resources -= 1; veterans_psi += 5*(1 - dissent);

우리는 우리의 재향 군인 복지 단체인 *가톨릭 상이군인 및 제대군인 전국연합*에 대한 지원금을 늘렸습니다.

@culture
choose-if: resources > 0
unavailable-subtitle: 자원이 부족합니다
subtitle: -1 자원
on-arrival: resources -= 1; maximalist_dissent -= 6; communist_dissent -= 6; labour_dissent -= 6; reformist_dissent -= 6; syndicalist_dissent -= 6 if syndicalist_active; socialist_sentiment += 2*(1-dissent);

[+ psi_name +]는 단순한 정당에 그쳐서는 안 됩니다. 우리가 운영하는 학교, 도서관, 문화 행사 및 기타 활동들은 당원들의 삶에 매우 중요합니다. 이러한 조직에 자금을 지원하면, 당의 정책이 마음에 들지 않더라도 당원들은 당에 더욱 헌신하게 될 것입니다.

@rg
choose-if: resources > 0
unavailable-subtitle: 자원이 부족합니다
subtitle: -1 자원
on-arrival: resources -= 1; rg_strength += 15*revolutionary_exhaustion; rg_militancy += 0.01*revolutionary_exhaustion; red_guards_timer = 3;

적위대 모병 활동을 강화했습니다.

@nothing

당 산하 조직에 자금을 지원하지 않습니다.

- @return: 계속...
