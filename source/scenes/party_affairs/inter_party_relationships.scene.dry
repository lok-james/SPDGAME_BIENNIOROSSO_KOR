title: 타 정당과의 관계
new-page: true
view-if: inter_party_relationships_timer <= 0 and (not_participating = 0 or prime_minister_party = "PNF")
tags: party_affairs
is-card: true
on-arrival: week_actions += 1; inter_party_relationships_timer = 3;
card-image: img/InterPartyRelationships.jpg

= 타 정당과의 관계

[+ psi_name +]는 아직 단독으로 국정을 운영할 수 없습니다. 역사적으로 연정이나 내각에 참여한 적이 없지만, 상황을 바꾸거나 더 개혁적인 노선을 택하려면 부르주아 정당들과의 관계 개선이 필수적입니다. 아니면 독자 노선을 계속 걸을 수도 있습니다.

- @liberals: 자유주의 정당인 [+ ldr_name +], [+ ul_name +]와 유대를 강화한다.
- @catholic_party: 가톨릭계 정당인 이탈리아 인민당과 유대를 강화한다.
- @leftists: 이념적 동지인 이탈리아 개혁사회당, [+ ds_name +] [? if pll_formed :, 자유노동당 ?],[? if not pri_prominent : ?] [? if pri_prominent : 그리고 이탈리아 공화당 ?]과 유대를 강화한다.
- @splinters: 우리에게서 분리된 정당들; [? if pcdi_formed : 이탈리아 공산당 ?] [? if pcdi_formed and psu_formed :, ?] [? if psu_formed : 단일사회당 ?] 과 유대를 강화한다.
- @psi_alone: The [+ psi_name +]는 당분간 독자 노선을 유지한다.
- @easy_discard
- @cancel_advisor_action

@splinters
subtitle: -1 자원
view-if: pcdi_formed or psu_formed
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; pcdi_relation += 8 if pcdi_formed; psu_relation += 8 if psu_formed;

옛 동지였던 분리된 정당들에 손을 내밀자, 적어도 그들은 우리를 바로 내치지는 않았습니다.

@leftists
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; psri_relation += 8; ds_relation += 8; ds_relation += 5 if ds_leader = "Giuffrida"; ds_relation -= 5 if ds_leader = "Carnazza"; communist_dissent += 4*(1-dissent); pri_relation += 5 if pri_prominent; ds_left += 0.5; pll_relation += 8; pri_relation -= 5 if pri_denounced and pri_prominent

우리는 이탈리아 개혁사회당과 [+ ds_name +][? if pll_formed :, 그리고 자유노동당 ?]과의 접촉을 강화하여 관계를 개선했습니다.

[? if pri_prominent and not pri_denounced : 이탈리아 공화당에도 접촉을 시도했으며, 그들은 따뜻하게 화답했습니다. ?]

[? if pri_prominent and pri_denounced : 이탈리아 공화당에 접촉을 시도했지만, 그들은 우리의 손길에 차갑게 응했습니다. ?]

@catholic_party
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1;
go-to: catholic_op if socialist_catholic_coalition == 0; catholic_coalition if socialist_catholic_coalition == 1

@catholic_op
on-arrival: ppi_relation += 5; communist_dissent += 4*(1-dissent); ppi_right -= 0.25; ppi_left += 0.25; republican_sentiment += 1; radicalisation -= 0.05;

현재 우리는 그들의 연정에 맞서는 야당이지만, 그렇다고 해서 호의적인 관계를 쌓는 것이 불가능한 것은 아닙니다.

@catholic_coalition
on-arrival: ppi_relation += 8; communist_dissent += 4*(1-dissent); ppi_left += 0.3; ppi_right -= 0.3; republican_sentiment += 3; radicalisation -= 0.1;

현재 우리는 그들과의 연정 파트너이므로, 관계 개선은 자연스러운 일입니다.

@liberals
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1;
go-to: liberal_op if psi_in_government == 0; liberal_coalition if centre_left_coalition == 1

@liberal_op
on-arrival: ldr_relation += 5; ul_relation += 5; communist_dissent += 4*(1-dissent); ldr_right -= 0.3; ul_nitti += 0.3; republican_sentiment += 1; radicalisation -= 0.05;

현재 우리는 그들의 연정에 맞서는 야당이지만, 그렇다고 해서 호의적인 관계를 쌓는 것이 불가능한 것은 아닙니다.

@liberal_coalition
on-arrival: ldr_relation += 8; ul_relation += 8; communist_dissent += 4*(1-dissent); ldr_right -= 0.5; ul_nitti += 0.5; republican_sentiment += 3; radicalisation -= 0.1;

현재 우리는 그들과의 연정 파트너이므로, 관계 개선은 자연스러운 일입니다.

@psi_alone

 수 없기 때문이든, 하려 하지 않기 때문이든, 지금 이 순간 다른 정당과 관계를 맺지 않습니다.

- @return: 계속하기...
