title: Media
new-page: true
is-card: true
tags: party_affairs
view-if: media_timer <= 0
on-arrival: week_actions += 1; media_timer = 6;
card-image: img/Media.jpg
#subtitle: Setting the direction for our newspapers and radio.

= Media

The PSI runs a veritable media empire, with many newspapers throughout every Italian city and region. We could use the media for various purposes...

- @commercial
- @campaign
- @ideological
- @radio
- @radio_2
- @radio_3
- @radio_4
- @return: We should not do anything with media at the moment.
- @easy_discard
- @cancel_advisor_action

@commercial
title: Make our newspapers more commercialized by broadening their appeal beyond party supporters.
subtitle: This might upset ideological purists, but it may bring in more funds and perhaps expose the middle class to socialist views.
on-arrival: resources += 1; middle_class_psi += 4*(1 - dissent); communist_dissent += 7; labour_dissent += 7

Our commercialized newspapers are able to bring in more funds. However, the left and labour factions express their displeasure.

@campaign
title: The purpose of our media is to elect more Social Democrats.
subtitle: Our newspapers will focus on the electoral campaign.
on-arrival: campaign_media += 1; workers_psi += 4*(1 - dissent);

Our campaign media is mostly reaching the workers who are already saturated with Socialist propaganda, but it must be doing *something*, or at least that is what we hope.

@ideological
title: We will have vibrant ideological debate within our newspapers.
subtitle: There will be space for all factions to air their views.
on-arrival: communist_dissent -= 6; labour_dissent -= 6; maximalist_dissent -= 6; reformist_dissent -= 6; 

This has been our media course historically, and it has successfully paved over the many tensions in our party.

@radio
title: Why not fund a radio station?
subtitle: -3 resources
choose-if: resources >= 3
unavailable-subtitle: Not enough resources.
view-if: radio = 0
on-arrival: radio += 1; resources -= 3; middle_class_psi += 2*(1 - dissent); industrialists_psi += 4*(1 - dissent); workers_psi += 2*(1 - dissent); labour_dissent += 5; socialist_sentiment += 3*(1 - dissent); nationalist_sentiment -= 3*(1 - dissent) if pacifism > 1; republican_sentiment += 3*(1-dissent) if democratisation > 1

A radio station would be one of the first of its kind in Italy, and the first socialist station in the nation. Funding is expensive, and radio's in it of itself seem only affordable to the richest of the middle class, alongside industrialists and landowners, who will be the main consumers of our content, it seems.

@radio_2
title: We must keep on building up our radio network.
subtitle: -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: radio > 0 and radio <= 3
on-arrival: radio += 1; resources -= 1; industrialists_psi += 5*(1 - dissent); middle_class_psi += 3*(1 - dissent); workers_psi += 1*(1 - dissent); socialist_sentiment += 3*(1 - dissent); nationalist_sentiment -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; republican_sentiment += 3*(1-dissent)*(democratisation-2) if democratisation > 2

Our new radio network has a mix of popular and political programming, and is proving a success.

@radio_3
title: We must keep on building up our radio network.
subtitle: It is now financially self-sufficient, and does not require any more resources.
view-if: radio > 3 and radio <= 5 and year <= 1921
on-arrival: radio += 1; industrialists_psi += 5*(1 - dissent); middle_class_psi += 4*(1 - dissent); workers_psi += 4*(1 - dissent); rural_psi += 3*(1 - dissent); veterans_psi += 2*(1-dissent); socialist_sentiment += 3*(1 - dissent); nationalist_sentiment -= 3*(1 - dissent)*(pacifism-2) if pacifism > 2; republican_sentiment += 3*(1-dissent)*(democratisation-2) if democratisation > 2

Our radio network has become a successful tool for spreading our message.

@radio_4
title: We should keep on supporting our radio network.
subtitle: As radio becomes more common, the novelty is beginning to wear off, and radio is becoming saturated in society. -1 resources
choose-if: resources >= 1
unavailable-subtitle: Not enough resources.
view-if: radio > 3 and (radio >= 6 or year >= 1922) and radio <= 9
on-arrival: radio += 1; resources -= 1; middle_class_psi += 3*(1 - dissent); industrialists_psi += 2*(1 - dissent); workers_psi += 3*(1 - dissent); rural_psi += 2*(1 - dissent); veterans_psi += 2*(1-dissent); socialist_sentiment += 2*(1 - dissent); nationalist_sentiment -= 2*(1 - dissent)*(pacifism-2) if pacifism > 2; republican_sentiment += 2*(1-dissent)*(democratisation-2) if democratisation > 2

We are continuing to invest in our radio stations. Both commercial and political radio are becoming more prevalent, primarily due to us, and competition is becoming fierce, it seems.
