title: 인터내셔널 내 관계
new-page: true
is-card: true
tags: party_affairs
view-if: international_relations_timer <= 0
on-arrival: week_actions += 1; international_relations_timer += 6;
card-image: img/Comintern.jpg
max-visits: 12
#subtitle: Building ties with our fraternal socialist parties.

= 코민테른 관계

노동사회주의 인터내셔널에서 우리는 유럽과 전 세계의 형제 사회주의 정당들과 연대를 구축할 수 있습니다.

- @fr_uk: 프랑스 사회당과 영국 노동당과의 관계를 심화하여 양국 정부가 이탈리아에 우호적인 태도를 유지하도록 유도한다.
- @germany: 독일 공산주의자들로부터 조직 운영과 시가전 기술을 습득한다.
- @comintern: 코민테른을 통해 소비에트와의 접촉을 넓히고 영향력을 확대한다.
- @papacy: 이탈리아 인민당 내 영향력을 활용하여 교황청과의 관계를 확대한다.
- @america: 미국 사회당과 접촉한다.
- @hungary: 헝가리 공산당에 원조를 제공한다.
- @return: 당분간 국제 관계는 필요하지 않다.
- @easy_discard
- @cancel_advisor_action

@fr_uk
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; entente_relation += 1; reformist_strength += 5; foreign_invasion_time += 6 if violent_revolution; supported_entente += 1;

프랑스와 영국의 사회주의자들은 각국 정부가 이탈리아에 더 우호적인 태도를 취하도록 영향력을 행사할 수 있습니다.

[? if violent_revolution : 우리에 대한 적대적 계획도 지연시킬 수 있습니다. ?]

@germany
subtitle: -1 자원
choose-if: resources >= 1 and soviet_germany != 1
unavailable-subtitle: [? if resources < 1 : 자원이 부족합니다. (1 필요) ?][? if soviet_germany : 스파르타쿠스단은 더는 위협을 받지 않습니다. ?]
on-arrival: communist_strength += 5; radicalisation += 0.3; rg_militancy += 0.04*revolutionary_exhaustion; resources -= 1; pcdi_bordiga += 1;

독일은 혼란에 휩싸여 있습니다. 스파르타쿠스 봉기의 기억이 생생한 가운데, 우익 세력의 위협을 받는 스파르타쿠스단 지도부에 피난처를 제공할 수 있습니다. 그들을 이탈리아 망명객으로 환영하고, 그들의 스파르타쿠스단 경험을 통해 우리 병력을 훈련시킬 수 있습니다.

@comintern
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; soviet_relation += 1; soviet_end_timer -= 1 if not rcw_over; soviet_recovery_timer -= 1 if rcw_over; pcdi_gramsci += 1; supported_ussr += 1; rg_militancy += 0.04*revolutionary_exhaustion if soviet_leader = "Trotsky"

우리의 노력은 소비에트의 환영을 받았고, [? if not rcw_over :백군에 맞선 내전에서 그들을 지원했습니다. ?][? if rcw_over :그들의 전후 복구를 지원했습니다. ?]

[? if soviet_leader = "Trotsky" : 이에 트로츠키 서기장이 적위대를 무장시킬 군사 장비를 보내왔습니다. ?]

@papacy
subtitle: -2 자원
choose-if: resources >= 2 and ppi_relation >= 55
unavailable-subtitle: [? if resources < 2 : 자원이 부족합니다. (2 필요) ?][? if ppi_relation < 55 : 이탈리아 인민당 내 영향력이 너무 낮습니다. ?]
on-arrival: papal_relation += 1; ppi_relation -= 10; resources -= 2; 

이탈리아 인민당 내 영향력을 활용하여 당내 접촉을 확대했습니다. 이로 인해 교황청의 태도가 호의적으로 변한 것을 감지했습니다.

@america
subtitle: -1 자원
choose-if: resources >= 1
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; supported_usa += 1;

미국 사회당은 소수 정당이며, 특히 최근 선거 패배 이후 지지 기반이 미미합니다. 우리가 그들을 위해 해줄 수 있는 것도, 반대로 그들이 우리를 위해 해줄 수 있는 것도 많지 않습니다.

@hungary
subtitle: -1 자원
choose-if: resources >= 1
view-if: year < 1920 and month <= 8
unavailable-subtitle: 자원이 부족합니다. (1 필요)
on-arrival: resources -= 1; supported_hungary += 1;

헝가리 공산주의자들은 정권 장악의 호기를 맞았으며, 우리의 원조가 그들의 대의를 확산하는 데 도움이 되기를 바랄 뿐입니다.