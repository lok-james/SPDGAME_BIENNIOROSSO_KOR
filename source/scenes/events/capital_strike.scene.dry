title: 자본 파업
subtitle: 이탈리아의 산업이 멈춰 서고 있습니다...
view-if: capital_strike_progress >= 10 and psi_in_government and not violent_revolution
new-page: true
max-visits: 1
on-arrival: unemployment += 4.6; veterans += 3; workers_psi*= 0.7; middle_class_psi *= 0.4; industrialists_psi *= 0.5; veterans_psi*= 0.7; rural_psi *= 0.8; republican_sentiment *= 0.7; ldr_relation -= 7*(ldr_moderates-ldr_right); ul_relation -= 7; ds_relation -= 4; ppi_relation -= 5; workers_pnf *= 1.3; veterans_pnf *= 1.3; middle_class_pnf *= 1.5; industrialists_pnf *= 2; rural_pnf *= 1.2; economic_growth -= 4.5; inflation -= 2.3; capital_strike_seen = 1;
tags: event

= 자본 파업

우리의 정책은 자본가들을 크게 격노시켰습니다. 투자는 끊겼고, 공장은 가동을 멈춰 노동자들이 일터로 돌아가지 못하게 하고 있습니다. 한편, 부르주아 언론들은 우리 당과 우리의 폭력적인 파업 및 점거가 경제를 마비시켰다며 연일 악의에 찬 비난을 퍼붓고 있습니다.

우리는 무엇을 할 수 있을까요?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: 노동자들이 공장을 장악하도록 하자!
unavailable-subtitle: [? if communist_strength <= reformist_strength: 당내 다수가 이 급진적인 조치를 지지하지 않습니다. ?] [? if judicial_reform < 3 : 사법부가 이 조치에 반대할 것입니다. ?] [? if rg_banned = 1 : 우리의 준군사조직이 금지되지 않았다면 가능했을지도 모릅니다. ?] [? if work_councils < 1 : 먼저 노동자 평의회의 지지를 확보해야 합니다. ?]
choose-if: communist_strength > reformist_strength and (judicial_reform >= 3 or rg_banned = 0) and work_councils >= 1
on-arrival: nationalisation_progress += 1; fasci_coup_progress += 6; unemployment -= 2; unemployment -= 1 if work_councils >= 3; veterans -= 1; workers += 1; workers_psi += 10*(1-dissent); veterans_psi += 10*(1-dissent); coalition_dissent += 2; ul_relation -= 4; ldr_relation -= 4; workers_pnf -= 8*(1-dissent); veterans_pnf -= 8*(1-dissent); communist_strength += 12; reformist_dissent += 6; communist_dissent -= 10; factory_takeovers += 1; economic_growth += 2.3; radicalisation += 1; revolutionary_fervour += 7; revolutionary_exhaustion += 0.1; king_relation -= 10;

자본가들이 우리를 공격한다면, 우리도 반격해야만 합니다. 우리는 적위대의 지원을 받아, 자본가들이 멈춰 세운 공장들을 노동자들이 직접 장악하고 완전히 통제할 수 있도록 권한을 부여했습니다. 물론 파시스트들은 우리를 도둑과 강도 떼라고 비난하고 있습니다.

@capital_controls
title: 충격을 완화하기 위해 자본 통제를 시행한다.
choose-if: judicial_reform >= 2
unavailable-subtitle: 사법부가 절대로 용납하지 않을 것입니다.
on-arrival: unemployment -= 1.4; workers_psi += 6*(1-dissent); veterans_psi += 6*(1-dissent); economic_growth += 1.7;

자본 통제를 시행하면 자본 파업이 가져올 최악의 경제적 충격을 일부 상쇄할 수 있을 것입니다.

@propaganda
title: 자본가에게 위기의 책임을 묻는 선전 캠페인을 전개한다.
subtitle: 자원 -1
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : 우리의 언론 기반이 충분하지 않습니다. ?] [? if resources < 1: 자원이 부족합니다. (1 필요) ?]
on-arrival: workers_psi += 7*(1-dissent); middle_class_psi += 6*(1-dissent); veterans_psi += 6*(1-dissent); republican_sentiment += 5*(1-dissent); resources -= 1

우리는 모든 언론 채널을 동원하여 경제 위기의 책임이 자본가들에게 있다는 역선전 캠페인을 시작합니다. 기존에 구축해둔 언론망 덕분에, 대중의 비난 화살을 우리에게서 자본가들에게로 돌리는 데 상당한 성공을 거두었습니다.

@nothing
title: 우리가 할 수 있는 일은 아무것도 없습니다.

헌법과 법의 테두리 안에서 우리가 할 수 있는 일은 없습니다.