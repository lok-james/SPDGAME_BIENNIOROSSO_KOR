title: 아체르보 법 폐지
view-if: acerbo_law_passed and psi_in_government and pnf_waiting = 0 and pnf_in_power = 0 and (pnf_leader = "Mussolini" or pnf_leader = "Balbo")
priority: -1
max-visits: 1
new-page: true
tags: event
achievement: follia_di_acerbo

= 아체르보 법 폐지

1923년 7월에 통과된 아체르보 법은 국가 파시스트당이 과반 의석 확보에 필요한 25% 득표율을 달성하지 못하면서 결국 엄청난 역풍을 맞았습니다. 이제 우리가 정권을 잡았으니, 이 부패한 법을 폐지하고 이탈리아에 다시 공정한 민주주의를 되찾을 수 있습니다.

- @remove_acerbo: 법률을 폐지한다.
- @keep_acerbo: 글쎄...

@remove_acerbo
subtitle: Such an unconstitutional law needs to go.
on-arrival: acerbo_law_passed = 0; next_election_time = time + 6 if next_election_time > time+6; next_election_month = month + 3 if next_election_time >= time+6; next_election_year = year if next_election_time >= time+6; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; 

아체르보 법은 손쉽게 폐지되었고, 이전의 동트 방식 선거 제도가 복원되었습니다. 보다 공정한 민의를 반영하기 위해 3개월 후 조기 총선이 실시됩니다.

- @root: 속 시원하군.

@keep_acerbo
view-if: psi_seating_disp >= 50
go-to: king_low if king_relation < 54; king_letter if king_relation >= 54

@king_letter

우리는 하원에서의 지배력을 유지하기 위해 아체르보 법을 유지하기로 결정했습니다.

그러나 최근, 아체르보 법의 유지 가능성에 대해 우려하는 이바노에 보노미의 편지가 [? if not reformists_defect : 필리포 투라티의 ?][? if reformists_defect : 조반니 바치의 ?] 사무실로 전달되었습니다. 발신인은 아직 확인되지 않았지만, 비토리오 에마누엘레 3세 국왕이 경고의 의미로 보낸 것으로 추정됩니다.

편지에는 법 폐지를 간청하는 내용이 담겨 있습니다. 이 법을 유지할 경우, 다른 정당들에게 [+ psi_name +]에 대한 대대적인 반대 선동의 빌미를 제공할 수 있으며, 우리 당은 이탈리아 정치권에서 고립될 수 있다는 것입니다. 비토리오 국왕은 우리의 정치적 동맹일지 모르나, 우리가 이처럼 불법적이고 기만적인 방식으로 권력을 잡으려 한다면 그조차도 우리를 변호할 수 없을 것입니다. 결국 반파시스트 연대를 처음 주창한 것은 우리였으며, 자칫하면 검은 셔츠단과 다를 바 없는 존재로 낙인찍힐 수 있습니다.

어떻게 해야 할까요?

- @king_low: 경고를 무시한다. 법은 유지되어야 한다.
- @remove_acerbo: 알겠다. 법을 폐지한다.

@king_low
on-arrival: royal_coup = 1
subtitle: 법을 유지함으로써 발생하는 후폭풍이 정말 그만한 가치가 있을까요?

이 결정은 우리 당 우파와 의회 내 동맹 정당들의 거센 반발을 불러일으켰고, 우리는 쿠데타를 시도했던 파시스트들과 다를 바 없다는 비난에 직면했습니다.

이미 국왕은 왕립군을 동원하여 우리 사무실과 본부를 급습하고 당 소속 의원들을 체포하기 시작했습니다. 우리는 반격해야만 합니다.

- @civil_war: 잠깐, 이게 무슨...